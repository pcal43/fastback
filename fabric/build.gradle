plugins {
    id "com.modrinth.minotaur" version "2.8.2"
    id "com.matthewprenger.cursegradle" version "1.4.0"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common
    shadowCommon

    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

archivesBaseName = "${project.archives_base_name}"
version = "${project.mod_version}-fabric"
group = project.maven_group


//OK THIS IS BASICALLY WORKING EXCEPT THAT THE SHADING IS NOT BEING APPLIED TO THE CLASSES FROM COMMON.
//SO WTF GRADLE
dependencies {

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }

    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // jgit
    include implementation("org.eclipse.jgit:org.eclipse.jgit:${project.jgit_version}") { transitive = false }

    // jgit needs this
    //   https://download.eclipse.org/jgit/site/6.4.0.202211300538-r/dependencies.html
    include runtimeOnly("com.googlecode.javaewah:JavaEWAH:${project.JavaEWAH_version}")  { transitive = false }

    // so jgit can do modern ssh
    include implementation("org.apache.sshd:sshd-core:${project.apache_sshd_version}") { transitive = false }
    runtimeOnly("org.apache.sshd:sshd-common:${project.apache_sshd_version}") { transitive = false }



    // this enables ed25519 support in apache_sshd
    // https://github.com/apache/mina-sshd/blob/dfa109b7b535d64e8ee395ddd0419e7696fb24ee/docs/dependencies.md
    include runtimeOnly("net.i2p.crypto:eddsa:${project.eddsa_version}") { transitive = false }

    // fabric-permissions-api (luckperms support)
    include modImplementation("me.lucko:fabric-permissions-api:${project.fabric_permissions_version}") { transitive = false }

    // Server Translations
    //   https://github.com/NucleoidMC/Server-Translations
    include modImplementation("xyz.nucleoid:server-translations-api:${project.server_translations_version}") { transitive = false }

    // Needed by Server Translations
    //   https://github.com/NucleoidMC/packet-tweaker
    include modRuntimeOnly("xyz.nucleoid:packet-tweaker:${project.packet_tweaker_version}") { transitive = false }

}

jar {
    dependsOn configurations.shadowCommon
    from {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        configurations.shadowCommon.collect {
            zipTree(it)
        }
    }
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

processResources {
    inputs.property "version", project.version
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}


// https://github.com/modrinth/minotaur
modrinth {
    token = System.getenv("MODRINTH_TOKEN") ?: 'MODRINTH_TOKEN_NOT_SET'
    projectId = "fastback"
    versionNumber = "${project.mod_version}"
    versionType = "release"
    uploadFile = remapJar
    changelog = "<p><a href='https://github.com/pcal43/fastback/releases/tag/${project.mod_version}'>https://github.com/pcal43/fastback/releases/tag/${project.mod_version}</a></p>"
    gameVersions = ["${project.minecraft_version}"]
    loaders = ["fabric"]
    dependencies {
        required.project "fabric-api"
    }
}


// https://github.com/matthewprenger/CurseGradle
curseforge {
    apiKey = System.getenv("CURSEFORGE_TOKEN") ?: 'CURSEFORGE_TOKEN_NOT_SET'

    project {
        id = "667417"
        releaseType = "release"
        changelog = "https://github.com/pcal43/fastback/releases/tag/${project.mod_version}"
        changelogType = "markdown"
        mod_version = project.mod_version
        addGameVersion ((String) project.minecraft_version)
        addGameVersion "Fabric"
        mainArtifact(remapJar)
        afterEvaluate {
            uploadTask.dependsOn("remapJar")
        }
    }

    options {
        forgeGradleIntegration = false
    }
}
